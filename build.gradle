plugins {
    id 'fabric-loom' version '1.0+'
    id "com.modrinth.minotaur" version "2.+"
    id 'com.matthewprenger.cursegradle' version '1.4.0'
}

apply plugin: 'fabric-loom'

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

var isStable = project.is_stable == "true"

archivesBaseName = "polymer-bundled"
version = project.mod_version + "+" + project.minecraft_version
group = project.maven_group

var field = com.google.gson.Gson.class.getDeclaredField("prettyPrinting");
field.setAccessible(true);
field.setBoolean(net.fabricmc.loom.LoomGradlePlugin.GSON, false);


System.out.println(isStable ? "release" : "beta")

allprojects {
    processResources {
        inputs.property "version", project.version

        filesMatching("fabric.mod.json") {
            expand "version": project.version
        }
    }
}

allprojects {
    apply plugin: 'fabric-loom'

    tasks.withType(JavaCompile).configureEach {
        it.options.release = 17
    }


    repositories {
        // Add repositories to retrieve artifacts from in here.
        // You should only use this when depending on other mods because
        // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
        // See https://docs.gradle.org/current/userguide/declaring_repositories.html
        // for more information about repositories.

        maven { url "https://jitpack.io" }
        maven { url "https://maven.nucleoid.xyz/" }
        maven { url "https://api.modrinth.com/maven" }
        maven { url "https://maven.gegy.dev/" }
        maven { url "https://maven.shedaniel.me/" }
        maven { url "https://maven.bai.lol" }
        maven { url "https://maven.ryanliptak.com/" }
        maven { url "https://api.modrinth.com/maven" }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url "https://maven.enginehub.org/repo/" }
        maven { url "https://maven.theepicblock.nl"; content { includeGroup("nl.theepicblock") } }
        maven { url 'https://maven.wispforest.io/' }
        maven { url "https://cursemaven.com" }
        //maven { url "https://dvs1.progwml6.com/files/maven/" }
        maven { url "https://maven.terraformersmc.com/releases" }
        maven { url "https://maven.theepicblock.nl" }
        maven { url "https://lazurite.dev/maven/releases" }
        maven { url "https://repo.opencollab.dev/maven-snapshots/" }
        maven { url 'https://maven.ladysnake.org/releases' }
        mavenLocal()
    }

    dependencies {
        // To change the versions see the gradle.properties file
        minecraft "com.mojang:minecraft:${project.minecraft_version}"
        mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
        modCompileOnly "net.fabricmc:fabric-loader:${project.loader_version}"
        modLocalRuntime "net.fabricmc:fabric-loader:${project.loader_version}"

        annotationProcessor("io.github.llamalad7:mixinextras-fabric:${mixin_extras_version}")
    }

    loom {
        enableTransitiveAccessWideners = false
        interfaceInjection {
            enableDependencyInterfaceInjection = false
        }
    }

    tasks.withType(JavaCompile).configureEach {
        // ensure that the encoding is set to UTF-8, no matter what the system default is
        // this fixes some edge cases with special characters not displaying correctly
        // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
        // If Javadoc is generated, this must be specified in that task too.
        it.options.encoding = "UTF-8"

        // The Minecraft launcher currently installs Java 8 for users, so your mod probably wants to target Java 8 too
        // JDK 9 introduced a new way of specifying this that will make sure no newer classes or methods are used.
        // We'll use that if it's available, but otherwise we'll use the older option.

        it.options.release = 17
    }

    java {
        // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
        // if it is present.
        // If you remove this line, sources will not be generated.
        withSourcesJar()
    }
}

subprojects {
    afterEvaluate {
        // Disable the gen sources task on sub projects
        genSourcesWithFernFlower.enabled = false
        genSourcesWithCfr.enabled = false
        unpickJar.enabled = false
    }
}

dependencies {
    api include(project(":polymer-autohost"))
    api include(project(":polymer-core"))
    api include(project(":polymer-common"))
    api include(project(":polymer-resource-pack"))
    api include(project(":polymer-blocks"))
    api include(project(":polymer-virtual-entity"))
    //include(project(":polymer-client-extensions"))
}

void setupRepositories(RepositoryHandler repositories) {
    def env = System.getenv()

    if (env.MAVEN_URL) {
        repositories.maven {
            credentials {
                username env.MAVEN_USERNAME
                password env.MAVEN_PASSWORD
            }
            url env.MAVEN_URL
        }
    } else {
        repositories.mavenLocal()
    }
}


curseforge {
    if (isStable && System.getenv("CURSEFORGE")) {
        apiKey = System.getenv("CURSEFORGE")

        project {
            id = "551890"
            releaseType = "release"
            changelog = System.getenv("CHANGELOG")
            changelogType = "markdown"
            addGameVersion ((String) project.minecraft_version)
            addGameVersion "Fabric"
            addGameVersion "Quilt"
            mainArtifact(remapJar)

            afterEvaluate {
                uploadTask.dependsOn("remapJar")
            }
        }
    }

    options {
        forgeGradleIntegration = false
    }
    remapJar {
        finalizedBy project.tasks.curseforge
    }
}

if (System.getenv("MODRINTH")) {
    modrinth {
        token = System.getenv("MODRINTH")
        projectId = 'xGdtZczs'// The ID of your modrinth project, slugs will not work.
        versionNumber = "" + version // The version of the mod to upload.
        versionType = isStable ? "release" : "beta"
        uploadFile = remapJar // This links to a task that builds your mod jar and sets "uploadFile" to the mod jar.
        gameVersions = [((String) project.minecraft_version)]
        changelog = System.getenv("CHANGELOG")
        loaders = ["fabric", "quilt"]
    }

    remapJar {
        finalizedBy project.tasks.modrinth
    }
}



// configure the maven publication

allprojects {
    apply plugin: 'maven-publish'

    processResources {
        doLast {
            fileTree(dir: outputs.files.asPath, include: "**/*.json").each {
                File file -> file.text = com.google.gson.JsonParser.parseString(file.text).toString()
            }
        }
    }

    classes {
        doLast {
            fileTree(dir: project.sourceSets.main.output.classesDirs.asPath, include: "**/*.json").each {
                File file -> file.text = com.google.gson.JsonParser.parseString(file.text).toString()
            }
        }
    }

    remapJar {
        doLast {
            fileTree(dir: outputs.files.asPath, include: "**/*.json").each {
                File file -> file.text = com.google.gson.JsonParser.parseString(file.text).toString()
            }
        }
    }

    afterEvaluate {
        if (project.version != "0.0.0-alpha.0") {
            publishing {
                publications {
                    mavenJava(MavenPublication) {
                        groupId = rootProject.maven_group
                        artifactId = project.archivesBaseName
                        version = project.version

                        from components.java
                    }

                }

                setupRepositories(repositories)
            }
        }
    }
}

